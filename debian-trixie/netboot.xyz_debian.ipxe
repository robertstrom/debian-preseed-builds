#!ipxe

# Debian Operating System
# http://www.debian.org
# The key component of this PXE boot file used in netboot.xyz is the modified kernel command line

goto ${menu}

:debian
set os Debian
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i386 ||
iseq ${os_arch} arm64 && set os_arch arm64 ||
clear debian_version
clear older_release
menu ${os} - ${os_arch}
item --gap Latest Releases
item bookworm ${space} ${os} 12.0 (bookworm)
item trixie ${space} ${os} 13.0 (trixie)
item --gap Testing Releases
item trixie ${space} ${os} trixie (testing)
item sid ${space} ${os} sid (unstable)
item --gap Older Releases
item older_release ${space} Set release codename...
choose debian_version || goto debian_exit
iseq ${debian_version} older_release && goto older_release ||
goto mirrorcfg

:older_release
set older_release true
set debian_mirror http://archive.debian.org
echo Setting mirror to ${debian_mirror}
clear debian_version
echo -n Please set enter code name of release: ${} && read debian_version
set dir ${debian_base_dir}/dists/${debian_version}/main/installer-${os_arch}/current/images/netboot/
goto deb_boot_type

:mirrorcfg
set debian_mirror ${debian_mirror}
set mirrorcfg mirror/suite=${debian_version}
set dir ${debian_base_dir}/dists/${debian_version}/main/installer-${os_arch}/current/images/netboot/
goto deb_boot_type

:deb_boot_type
menu ${os} [${debian_version}] Installer
item --gap Install types
item text ${space} Text Based Install
item graphical ${space} Graphical Based Install
item rescue ${space} Rescue Mode
item expert ${space} Expert Install
# item preseed ${space} Specify preseed url...
choose --default ${type} type || goto debian

echo ${cls}
goto deb_${type}

:deb_rescue
set install_params rescue/enable=true
goto deb_text

:deb_expert
set install_params priority=low
goto deb_text

# :deb_preseed
# echo -n Specify preseed URL for ${os} ${debian_version}: && read preseedurl
# set install_params auto=true priority=critical preseed/url=${preseedurl}
# goto deb_text

:deb_text
set dir ${dir}${menu}-installer/${os_arch}
goto deb_boot

:deb_graphical
set dir ${dir}gtk/${menu}-installer/${os_arch}
set install_params vga=788
goto deb_boot

:deb_boot
imgfree
# kernel ${debian_mirror}/${dir}/linux ${install_params} ${netcfg} ${mirrorcfg} initrd=initrd.magic ${cmdline}
kernel ${debian_mirror}/${dir}/linux auto=true preseed/url=http://192.168.0.82/debian-kde-preseed.cfg interface=auto append auto=true priority=high netcfg/get_hostname?= netcfg/get_domain?=.local
initrd ${debian_mirror}/${dir}/initrd.gz
echo
echo MD5sums:
md5sum linux initrd.gz
boot

:debian_exit
clear menu
exit 0
